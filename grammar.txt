WIP grammar for the gremlin language
() denotes 1 or more repititions
{} denotes 0 or more repititions
[] denotes an optional sequence
<> denotes a range of characters or values
\a denotes an alphabetical character
\c denotes a character
\d denotes a digit

IDENT  -> (\a | '_'){\a | '_' | \d}
STRING -> '"' {\c} '"'

TYPE_NAME -> IDENT
           | "var"

TYPE -> {'[' EXPRESSION ']'} {'*'} TYPE_NAME
      | '('[IDENT ':' TYPE {',' IDENT ':' TYPE}]')' ["->" TYPE]
     
MODULE -> {COMPILER_DIRECTIVE | USER_LEVEL_NOTE | CONSTANT_DECLARATION | VARIABLE_DECLARATION | TYPE_DECLARATION | FUNCTION_DECLARATION}

COMPILER_DIRECTIVE -> '#' IDENT [STRING | COMPOUND_STATEMENT]
USER_LEVEL_NOTE -> '@' IDENT

CONSTANT_DECLARATION -> IDENT "::" EXPRESSION ';'

VARIABLE_DECLARATION -> IDENT ':' TYPE ['=' EXPRESSION] ';'
                      | IDENT ':' '=' EXPRESSION ';'

TYPE_DECLARATION -> IDENT "::" "struct" COMPOUND_STATEMENT
                  | IDENT "::" "union"  COMPOUND_SATEMENT

FUNCTION_DECLARATION_ARGUMENT_LIST -> {IDENT ':' TYPE ['=' EXPRESSION]}
FUNCTION_DECLARATION -> IDENT "::" '(' FUNCTION_DECLARATION_ARGUMENT_LIST ')' ["->" TYPE] COMPOUND_STATEMENT

STATEMENT -> ';'
           | EXPRESSION ';'
           | COMPOUND_STATEMENT
           | "if" '(' EXPRESSION ')' STATEMENT ["else" STATEMENT]
           | "while" '(' EXPRESSION ')' STATEMENT
           | return EXPRESSION ';'
           | defer STATEMENT

ASSIGNMENT_OPERATOR -> "+=" | "-=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "&=" | "|=" | "^=" | "="
UNARY_OPERATOR      -> "&" | "*" | "+" | "-" | "~" | "!"

EXPRESSION -> ASSIGNMENT_EXPRESSION
            | ASSIGNMENT_EXPRESSION ',' EXPRESSION

ASSIGNMENT_EXPRESSION -> CONDITIONAL_EXPRESSION
                       | UNARY_EXPRESSION ASSIGNMENT_OPERATOR ASSIGNMENT_EXPRESSION

CONDITIONAL_EXPRESSION -> LOGICAL_OR_EXPRESSION
                        | LOGICAL_OR_EXPRESSION '?' EXPRESSION ':' CONDITIONAL_EXPRESSION

LOGICAL_OR_EXPRESSION -> LOGICAL_AND_EXPRESSION
                       | LOGICAL_AND_EXPRESSION "||" LOGICAL_OR_EXPRESSION

LOGICAL_AND_EXPRESSION -> INCLUSIVE_OR_EXPRESSION
                        | INCLUSIVE_OR_EXPRESSION "&&" LOGICAL_AND_EXPRESSION

INCLUSIVE_OR_EXPRESSION -> EXCLUSIVE_OR_EXPRESSION
                         | EXCLUSIVE_OR_EXPRESSION '|' INCLUSIVE_OR_EXPRESSION

EXCLUSIVE_OR_EXPRESSION -> AND_EXPRESSION
                         | AND_EXPRESSION '^' EXCLUSIVE_OR_EXPRESSION

AND_EXPRESSION -> EQUALITY_EXPRESSION
                | EQUALITY_EXPRESSION '&' AND_EXPRESSION

EQUALITY_EXPRESSION -> RELATIONAL_EXPRESSION
                     | RELATIONAL_EXPRESSION "==" EQUALITY_EXPRESSION
                     | RELATIONAL_EXPRESSION "!=" EQUALITY_EXPRESSION

RELATIONAL_EXPRESSION -> SHIFT_EXPRESSION
                       | SHIFT_EXPRESSION '<' RELATIONAL_EXPRESSION
                       | SHIFT_EXPRESSION '>' RELATIONAL_EXPRESSION
                       | SHIFT_EXPRESSION "<=" RELATIONAL_EXPRESSION
                       | SHIFT_EXPRESSION ">=" RELATIONAL_EXPRESSION

SHIFT_EXPRESSION -> ADDITIVE_EXPRESSION
                  | ADDITIVE_EXPRESSION "<<" SHIFT_EXPRESSION
                  | ADDITIVE_EXPRESSION ">>" SHIFT_EXPRESSION

ADDITIVE_EXPRESSION -> MULTIPLICATIVE_EXPRESSION
                     | MULTIPLICATIVE_EXPRESSION '+' ADDITIVE_EXPRESSION
                     | MULTIPLICATIVE_EXPRESSION '-' ADDITIVE_EXPRESSION

MULTIPLICATIVE_EXPRESSION -> CAST_EXPRESSION
                           | CAST_EXPRESSION '*' MULTIPLICATIVE_EXPRESSION
                           | CAST_EXPRESSION '/' MULTIPLICATIVE_EXPRESSION
                           | CAST_EXPRESSION '%' MULTIPLICATIVE_EXPRESSION



CAST_EXPRESSION -> UNARY_EXPRESSION
                 | "cast" '(' TYPE ')' CAST_EXPRESSION

UNARY_EXPRESSION -> POSTFIX_EXPRESSION
                  | "++" UNARY_EXPRESSION
                  | "--" UNARY_EXPRESSION
                  | UNARY_OPERATOR CAST_EXPRESSION

POSTFIX_EXPRESSION -> PRIMARY_EXPRESSION
                    | POSTFIX_EXPRESSION '[' EXPRESSION ']'
                    | POSTFIX_EXPRESSION '(' ')'
                    | POSTFIX_EXPRESSION '(' ARGUMENT_EXPRESSION_LIST ')'
                    | POSTFIX_EXPRESSION '.' IDENT
                    | POSTFIX_EXPRESSION "++"
                    | POSTFIX_EXPRESSION "--"

PRIMARY_EXPRESSION -> IDENTIFIER
                    | CONSTANT
                    | STRING
                    | '(' EXPRESSION ')'